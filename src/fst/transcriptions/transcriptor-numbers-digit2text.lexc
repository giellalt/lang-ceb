! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2015 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

Multichar_Symbols

  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
  +String    ! Tag to denote non-numeric strings
  +NumNum    ! Tag to denote real numbers in one form or another

LEXICON Root

!These 4 lines are for TTS, they are commented out.
!< %+String [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|â|ê|î|ô|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Â|Ê|Î|Ô]* > ENDLEX ; ! This first line is to allow all letter strings.
!+NumNum NUMBERSECTION ;
!+NumNum ENDLEXSECTION ;
!LEXICON NUMBERSECTION

HUNDREDSMILLION ;		! 100M

TENSMILLION ;			! 10-99M
ONESMILLION ;			! 1-9M
HUNDREDSTHOUSAND ;		! 100000-999999 NB
TENSTHOUSAND ;			! 11000-99999,10000-10999
ONESTHOUSAND ;			! 1000-9999
ONESHUNDRED ;		! 100-999
TENS ;			! 10-99
ONES ;			! 1-9

LEXICON HUNDREDSMILLION
1:usa%  HUNDREDMILLION ;	! 100M
2:duha%  HUNDREDMILLION ;
3:tulo%  HUNDREDMILLION ;
4:upat%  HUNDREDMILLION ;
5:lima%  HUNDREDMILLION ;
6:unom%  HUNDREDMILLION ;
7:pito%  HUNDREDMILLION ;
8:walo%  HUNDREDMILLION ;
9:siyam%  HUNDREDMILLION ;

LEXICON HUNDREDMILLION
:ka% gatos% ka%    TENSMILLION ;
%0:ka% gatos% ka%   ONESMILLION ;
%0%0:ka% gatos% ka%  MILLION ;


LEXICON TENSMILLION
1:napulo TENMILLION ;
2:kaluhaan TENMILLION ;
3:katloan TENMILLION ;
4:kap-atan TENMILLION ;
5:kalim-an TENMILLION ;
6:kan-oman TENMILLION ;
7:kapitoan TENMILLION ;
8:kawaloan TENMILLION ;
9:kasiyaman TENMILLION ;

LEXICON TENMILLION
: MILLION ;
:% ug%  ONESMILLION ;

LEXICON ONESMILLION
1:usa MILLION ;
2:duha MILLION ;
3:tulo MILLION ;
4:upat MILLION ;
5:lima MILLION ;
6:unom MILLION ;
7:pito MILLION ;
8:walo MILLION ;
9:siyam MILLION ;

LEXICON MILLION
:% ka% milyon% ug%   HUNDREDSTHOUSAND ;              ! 100000-999999 NB
%0:% ka% milyon% ug%   TENSTHOUSAND ;                  ! 10000-99999
%0%0:% ka% milyon% ug%   ONESTHOUSAND ;                  ! 1000-9999
%0%0%0:% ka% milyon% ug%   ONESHUNDRED ;           ! 100-999
%0%0%0%0:% ka% milyon% ug%   TENS ;
%0%0%0%0%0:% ka% milyon% ug%   ONES ;
%0%0%0%0%0%0:% ka% milyon  ENDLEX ;
! =================
! Under the million
! =================

LEXICON HUNDREDSTHOUSAND
1:usa%  HUNDREDTHOUSAND ; ! 100000-199999
2:duha%  HUNDREDTHOUSAND ;
3:tulo%  HUNDREDTHOUSAND ;
4:upat%  HUNDREDTHOUSAND ;
5:lima%  HUNDREDTHOUSAND ;
6:unom%  HUNDREDTHOUSAND ;
7:pito%  HUNDREDTHOUSAND ;
8:walo%  HUNDREDTHOUSAND ;
9:siyam%  HUNDREDTHOUSAND ;


LEXICON HUNDREDTHOUSAND	! X = 1-9, Y = 0-9
:ug%  TENSTHOUSAND ;			! X2XYYY, X10YYY
%0%0: THOUSANDS ;	! X00YYY

LEXICON TENSTHOUSAND
1:napulo TENTHOUSAND ;
2:kaluhaan TENTHOUSAND ;
3:katloan TENTHOUSAND ;
4:kap-atan TENTHOUSAND ;
5:kalim-an TENTHOUSAND ;
6:kan-oman TENTHOUSAND ;
7:kapitoan TENTHOUSAND ;
8:kawaloan TENTHOUSAND ;
9:kasiyaman TENTHOUSAND ;


LEXICON TENTHOUSAND
%0: THOUSAND ;
:% ug%  ONESTHOUSAND ; 



LEXICON ONESTHOUSAND      ! x > 1
1:usa%  THOUSAND ;
2:duha%  THOUSAND ;
3:tulo%  THOUSAND ;
4:upat%  THOUSAND ;
5:lima%  THOUSAND ;
6:unom%  THOUSAND ;
7:pito%  THOUSAND ;
8:walo%  THOUSAND ;
9:siyam%  THOUSAND ;

LEXICON THOUSAND
:ka% libo% ug%  ONESHUNDRED ;	   ! 1100-1999
%0:ka% libo% ug%  TENS ;	   ! 1010-1099
%0%0:ka% libo% ug%  ONES ;	   ! 1001-1009
%0%0%0:ka% libo ENDLEX ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON ONESHUNDRED
1:usa HUNDRED ;
2:duha HUNDRED ;
3:tulo HUNDRED ;
4:upat HUNDRED ;
5:lima HUNDRED ;
6:unom HUNDRED ;
7:pito HUNDRED ;
8:walo HUNDRED ;
9:siyam HUNDRED ;

LEXICON HUNDRED
:% ka% gatos% ug%   TENS ;
%0:% ka% gatos% ug%   ONES ;
%0%0:% ka% gatos ENDLEX ;

LEXICON TENS
1:napulo TEN ;
2:kaluhaan TEN ;
2:kawhaan+Use/NG TEN ;
3:katloan TEN ;
3:katuloan+Use/NG TEN ;
4:kap-atan TEN ;
5:kalim-an TEN ;
6:kan-oman TEN ;
7:kapitoan TEN ;
8:kawaloan TEN ;
9:kasiyaman TEN ;

LEXICON TEN
%0: ENDLEX ;
:% ug%  ONES ;

LEXICON ONES
CARDINAL ;

LEXICON CARDINAL
1:usa ENDLEX ;
2:duha ENDLEX ;
3:tulo ENDLEX ;
4:upat ENDLEX ;
5:lima ENDLEX ;
6:unom ENDLEX ;
7:pito ENDLEX ;
8:walo ENDLEX ;
9:siyam ENDLEX ;

!LEXICON ENDLEX
! ENDLEX ;
! ENDLEXSECTION ;
!
!LEXICON ENDLEXSECTION  ! 
!                  ENDLEX ;
!! % komma% :,      Root ;
!! % tjuohkkis% :%. Root ;
!! % kolon% :%:     Root ;
!! % sárggis% :%-   Root ; 
! % la% :%=         Root ; 
! % gráda% :%°      Root ; 
! % paragráfa% :§   Root ; 
!! % násti% :%*     Root ; 
! % ja% :&          Root ; 

LEXICON ENDLEX
 # ;
